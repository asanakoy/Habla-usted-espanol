source ../common.foma

define GuessLexicon Stem [ 
	[o r "+N"]:[o r [ 0 | "*" | "^" s | "*" "^" s ] ]	|
	[i o n "+N"]:[i o n  [ 0 | "*" | "^" s | "*" "^" s ] ]	|
	[a l "+N"]:[a l [ 0 | "*" | "^" s | "*" "^" s ] ]	|
	[o "+N"]:[o [ 0 | "*" | "^" s | "*" "^" s ]  ]          |		
	["+N"]:["^" s | "*" "^" s] ] ;


define MascToFemLastToA  e -> a || _ "*" ,,
			 o -> a || _ "*" ;

define PlusAEndings [ o r | t o r | d o r | i o n | a l];

define MascToFemEndAInsertion [..] -> a || PlusAEndings _ "*";

define PluralEInsertion [..] -> e || WhenInsertE "^" _ s; 

define CReplacement c -> qu || _ "^";

define ZXReplacement z -> c || _ "^",,
		     x -> c || _ "^";

define GuessGrammar  GuessLexicon 	.o. 
      		MascToFemLastToA 	.o. 
		MascToFemEndAInsertion 	.o. 
		StarDelete 		.o. 
		PluralEInsertion 	.o. 
		CReplacement  		.o. 
		ZXReplacement 		.o. 
		HatDelete;

regex GuessGrammar;

save stack GuesserSustantivo.bin

quit;
